; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
; #define VERSION "0.1.1" 
[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{5ADAD22B-E0B0-41EC-9527-DB06F04AAAE4}
AppName=PREDA Language Preview Toolchain
AppPublisher=PREDA Dev Team
AppPublisherURL=https://www.preda-lang.org
DefaultDirName={localappdata}\PREDA\PREDA_Dev_Environment
DefaultGroupName=PREDA
OutputDir=.\..\..\bundle
OutputBaseFilename=preda-toolchain
SetupIconFile=assets\logo.ico
Compression=lzma
SolidCompression=yes
AppCopyright=Copyright (C) PREDA Dev Team 2022
AllowUNCPath=False
ArchitecturesInstallIn64BitMode=x64
ArchitecturesAllowed=x64
UsePreviousPrivileges=False
MinVersion=6.1sp1
DisableProgramGroupPage=yes
PrivilegesRequired=lowest
UninstallDisplayIcon={app}\bin\chsimu.exe
UninstallDisplayName=PREDA Language Preview Toolchain
AppVersion={#VERSION}
VersionInfoVersion={#VERSION}
SetupLogging=yes
DisableWelcomePage=False
WizardSmallImageFile=assets\92_97.bmp
WizardImageFile=assets\273_556.bmp
SetupMutex=Preda_Dev

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

; [Tasks]
; Name: "default"; Description: "Default installation"; Components: FullInstall;

[Type]
Name: "Full"; Description: "Full installation"
Name: "Custom"; Description: "Custom installation"; Flags: iscustom

[Components]
Name: "FullInstall"; Description: "Native installation"; Types: Full Custom; Flags: fixed
Name: "Wasm"; Description: "Wasm installation"; Types: full; Flags: disablenouninstallwarning


[Files]
Source: ".\..\..\bundle\PREDA\bin\*.exe"; DestDir: "{app}\bin"; Components: FullInstall; Flags: ignoreversion recursesubdirs createallsubdirs
Source: ".\..\..\bundle\PREDA\bin\transpiler.dll"; DestDir: "{app}\bin"; Components: FullInstall; Flags: ignoreversion recursesubdirs createallsubdirs
Source: ".\..\..\bundle\PREDA\bin\evmone.dll"; DestDir: "{app}\bin"; Components: FullInstall; Flags: ignoreversion recursesubdirs createallsubdirs
Source: ".\..\..\bundle\PREDA\bin\preda_engine.dll"; DestDir: "{app}\bin"; Components: FullInstall; Flags: ignoreversion recursesubdirs createallsubdirs
Source: ".\..\..\bundle\PREDA\compile_env\*"; DestDir: "{app}\compile_env"; Components: FullInstall; Flags: ignoreversion recursesubdirs createallsubdirs
Source: ".\..\..\bundle\PREDA\docs\*"; DestDir: "{app}\docs"; Components: FullInstall; Flags: ignoreversion recursesubdirs createallsubdirs
Source: ".\..\..\bundle\PREDA\examples\*"; DestDir: "{app}\examples"; Components: FullInstall; Flags: ignoreversion recursesubdirs createallsubdirs
Source: ".\..\..\bundle\PREDA\extensions\*"; DestDir: "{app}\extensions"; Components: FullInstall; Flags: ignoreversion recursesubdirs createallsubdirs
Source: ".\..\..\bundle\PREDA\scripts\*"; DestDir: "{app}\scripts"; Components: FullInstall; Flags: ignoreversion recursesubdirs createallsubdirs
Source: ".\..\..\bundle\PREDA\mingw64\*"; DestDir: "{app}\mingw64"; Components: FullInstall; Flags: ignoreversion recursesubdirs createallsubdirs

Source: ".\..\..\bundle\PREDA\emscripten\*"; DestDir: "{app}\emscripten"; Components: Wasm; Flags: ignoreversion recursesubdirs createallsubdirs
Source: ".\..\..\bundle\PREDA\bin\wasmtime.dll"; DestDir: "{app}\bin"; Components: Wasm; Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]

[Messages]

[InstallDelete]
; Type: filesandordirs; Name: "{app}\bin\wasmtime.dll"
Type: filesandordirs; Name: "{app}\emscripten"

[Run]
Filename: "powershell.exe"; \
  Parameters: "-ExecutionPolicy Bypass -File ""{app}\scripts\InstallExtension.ps1"" -ExtensionPath ""{app}\extensions\preda.vsix"""; \
  WorkingDir: {app}; \
  Flags: runhidden

Filename: "powershell.exe"; \
  Parameters: "-ExecutionPolicy Bypass -File ""{app}\scripts\ConfigureVSCode.ps1"" -BinPath ""{app}\bin\chsimu.exe"""; \
  WorkingDir: {app}; \
  Flags: runhidden

Filename: "powershell.exe"; \
  Parameters: "-ExecutionPolicy Bypass -File ""{app}\scripts\ConfigureWasmtime.ps1"" -AppPath ""{app}\bin"" -wasmString {code:GetWasmSelectedValue}"; \
  WorkingDir: {app}; \
  Flags: runhidden

Filename: "powershell.exe"; \
  Parameters: "-ExecutionPolicy Bypass -File ""{app}\scripts\OpenContractFolder.ps1"" -Path ""{app}\examples"" -MDFile ""{app}\docs\toolchain_user_manual.md"""; \
  WorkingDir: {app}; \
  Description: "Start VSCode and Open Folder of PREDA code examples"; \
  Flags: postinstall nowait skipifsilent runhidden 

[UninstallDelete]
Type: filesandordirs; Name: "{app}"

; [UninstallDelete]
; Type: files; Name: "{app}\bin\config.json"

[Code]
// Determine whether to choose 'Wasm' or 'FullInstall'
procedure CurStepChanged(CurStep: TSetupStep);
var 
  WasmSelected: Boolean;
begin
  if CurStep = ssPostInstall then
  begin
    WasmSelected := WizardIsComponentSelected('Wasm');
  end;
end;

function GetWasmSelectedValue(Param: string): string;
begin
  if WizardIsComponentSelected('Wasm') then
    Result := 'true'
  else
    Result := 'false';
end;

function GetVSCodeInstallPath(): string;
var
  path: string;
begin

  // VSCODE64USER
  if RegQueryStringValue(HKEY_CURRENT_USER, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{771FD6B0-FA20-440A-A002-3B3BAC16DC50}_is1', 'InstallLocation', path) and DirExists(path) then
    result:= path;

  // VSCODE32USER
  if RegQueryStringValue(HKEY_CURRENT_USER, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{D628A17A-9713-46BF-8D57-E671B46A741E}_is1', 'InstallLocation', path) and DirExists(path) then
    result:= path;

  // VSCODE32SYSTEM
  if RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall\{F8A2A208-72B3-4D61-95FC-8A65D340689B}_is1', 'InstallLocation', path) and DirExists(path) then
    result:= path;

  // VSCODE64SYSTEM
  if RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{EA457B21-F73E-494C-ACAB-524FDE069978}_is1', 'InstallLocation', path) and DirExists(path) then
    result:= path;

end;

function NextButtonClick(CurPageID: Integer): Boolean;
var
  vscode_path: string;
  mbr: Integer;
  ErrCode: Integer;
begin
  LOG('id:'+inttostr(CurPageID));

  if (CurPageID<>wpWelcome) then
  begin
    result:=true;
    exit;
  end;

  vscode_path:=GetVSCodeInstallPath();
  LOG(vscode_path);

  if Length(vscode_path)=0 then
  begin
    mbr := MsgBox('This application requires VSCode. Please install the VSCode then run this installer again.', mbInformation, MB_OKCANCEL);
    if (mbr= IDOK) then
      ShellExec('open', 'https://code.visualstudio.com/Download', '', '', SW_SHOW, ewNoWait, ErrCode);
    exit;
  end;

  result:=true;
end;
